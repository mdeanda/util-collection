buildscript {
    repositories {
        maven {
            url "${artifactory_plugins}"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.jfrog.artifactory-upload'
apply plugin: "com.jfrog.artifactory"

group = 'com.thedeanda'
version = '1.3-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
	manifest.attributes provider: 'gradle'
}

artifactory {
	contextUrl = "${artifactory_contextUrl}"
	publish {
		repository {
			if (version.matches(/[0-9\.]+$/)) {
				repoKey = 'libs-release-local'
			} else {
				repoKey = 'libs-snapshot-local'
			}
			username = "${artifactory_user}" //The publisher user name
			password = "${artifactory_password}" //The publisher password
		}
		defaults {
			publishConfigs('archives', 'published')
			//properties = ['build.status': "$it.project.status".toString()]
			publishPom = true //Publish generated POM files to Artifactory (true by default)
			publishIvy = false //Publish generated Ivy descriptor files to Artifactory (true by default)
		}
	}
	resolve {
		repository {
			repoKey = 'libs-release' //The Artifactory (preferably virtual) repository key to resolve from
			username = "${artifactory_user}" //Optional resolver user name (leave out to use anonymous resolution)
			password = "${artifactory_password}" //The resolver password
			maven = true
		}
	}

}

dependencies {
	compile group: 'org.slf4j', name: 'slf4j-api', version:'1.+'
	compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.3.2'
	compile group: 'commons-io', name: 'commons-io', version:'2.4'
	testCompile group: 'junit', name: 'junit', version:'4.11'
	testCompile 'org.slf4j:slf4j-simple:1.+'
}
